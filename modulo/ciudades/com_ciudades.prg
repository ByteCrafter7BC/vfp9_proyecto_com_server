**/
* com_ciudades.prg
*
* Derechos de autor (C) 2000-2025 ByteCrafter7BC <bytecrafter7bc@gmail.com>
*
* Este programa es software libre: puede redistribuirlo y/o modificarlo
* bajo los términos de la Licencia Pública General GNU publicada por
* la Free Software Foundation, ya sea la versión 3 de la Licencia, o
* (a su elección) cualquier versión posterior.
*
* Este programa se distribuye con la esperanza de que sea útil,
* pero SIN NINGUNA GARANTÍA; sin siquiera la garantía implícita de
* COMERCIABILIDAD o IDONEIDAD PARA UN PROPÓSITO PARTICULAR. Consulte la
* Licencia Pública General de GNU para obtener más detalles.
*
* Debería haber recibido una copia de la Licencia Pública General de GNU
* junto con este programa. Si no es así, consulte
* <https://www.gnu.org/licenses/>.
*/

DEFINE CLASS com_ciudades AS com_base OF com_base.prg OLEPUBLIC
    cModelo = 'ciudades'

    **--------------------------------------------------------------------------
    FUNCTION nombre_existe(tcNombre AS String, tnDepartamen AS Integer) ;
            AS Logical ;
        HELPSTRING 'Devuelve verdadero (.T.) si el nombre existe u ocurre un error; de lo contrario, devuelve falso (.F.).'
        RETURN THIS.oRepositorio.nombre_existe(tcNombre, tnDepartamen)
    ENDFUNC

    **--------------------------------------------------------------------------
    FUNCTION sifen_existe(tnSifen AS Integer) AS Logical ;
        HELPSTRING 'Devuelve verdadero (.T.) si el código del SIFEN existe u ocurre un error; de lo contrario, devuelve falso (.F.).'
        RETURN THIS.oRepositorio.sifen_existe(tnSifen)
    ENDFUNC

    **--------------------------------------------------------------------------
    FUNCTION obtener_por_nombre(tcNombre AS String, tnDepartamen AS Integer) ;
            AS Object ;
        HELPSTRING 'Devuelve un objeto (Object) si el nombre existe; de lo contrario, devuelve falso (.F.). En caso de error, devuelve falso (.F.).'
        RETURN THIS.oRepositorio.obtener_por_nombre(tcNombre, tnDepartamen)
    ENDFUNC

    **--------------------------------------------------------------------------
    FUNCTION obtener_por_sifen(tnSifen AS Integer) AS Object ;
        HELPSTRING 'Devuelve un objeto (Object) si el código del SIFEN existe; de lo contrario, devuelve falso (.F.). En caso de error, devuelve falso (.F.).'
        RETURN THIS.oRepositorio.obtener_por_sifen(tnSifen)
    ENDFUNC

    **/ ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ *
    *                            PROTECTED METHODS                            *
    * ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ */

    **--------------------------------------------------------------------------
    PROTECTED FUNCTION convertir_dto_a_modelo
        LPARAMETERS toDto

        IF VARTYPE(toDto) != 'O' THEN
            RETURN .F.
        ENDIF

        LOCAL lnCodigo, lcNombre, lnDepartamen, lnSifen, llVigente

        WITH toDto
            lnCodigo = .obtener_codigo()
            lcNombre = ALLTRIM(.obtener_nombre())
            lnDepartamen = .obtener_departamen()
            lnSifen = .obtener_sifen()
            llVigente = .esta_vigente()
        ENDWITH

        RETURN NEWOBJECT(THIS.cModelo, THIS.cModelo + '.prg', '', ;
            lnCodigo, lcNombre, lnDepartamen, lnSifen, llVigente)
    ENDFUNC
ENDDEFINE
